// http://mike-neck.github.io/blog/2013/06/21/how-to-publish-artifacts-with-gradle-maven-publish-plugin-version-1-dot-6/
// apply plugin: 'maven-publish' // https://gradle.org/docs/current/userguide/publishing_maven.html

// http://central.sonatype.org/pages/ossrh-guide.html#deployment
// -> http://central.sonatype.org/pages/gradle.html 
// -> http://yennicktrevels.com/blog/2013/10/11/automated-gradle-project-deployment-to-sonatype-oss-repository/
// -> http://central.sonatype.org/pages/ossrh-guide.html#create-a-ticket-with-sonatype


buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

apply plugin: 'java' // http://gradle.org/docs/current/userguide/tutorial_java_projects.html
apply plugin: 'eclipse'
apply plugin: 'signing' // http://gradle.org/docs/current/userguide/signing_plugin.html
apply plugin: 'maven' // https://gradle.org/docs/current/userguide/artifact_management.html
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'
    
// http://gradle.org/docs/current/userguide/tutorial_this_and_that.html#sec:gradle_properties_and_system_properties
// http://central.sonatype.org/pages/working-with-pgp-signatures.html

sourceCompatibility = 1.8

/*
// http://gradle.org/docs/current/dsl/org.gradle.plugins.ide.eclipse.model.EclipseClasspath.html
eclipse {
	classpath {
		// http://stackoverflow.com/questions/11901737/specifiy-jre-container-with-gradle-eclipse-plugin
		containers.clear()
		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}
*/

group = 'com.github.kristiankime'
version = '0.0.15'
archivesBaseName = "functional-collections"

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'com.google.guava', name: 'guava', version: '18.0'
	
	testCompile group: 'org.apache.commons', name: 'commons-lang3',    version: '3.4'
	testCompile group: 'junit',              name: 'junit',            version: '4.12'
	testCompile group: 'org.hamcrest',       name: 'hamcrest-library', version: '1.3'
	
	provided group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar

	archives javadocJar
	archives sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
			  authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
			  authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			pom.project {
			   name 'FCollections'
			   packaging 'jar'
			   description 'FCollections is a series of function style Java Collections'
			   url 'https://github.com/kristiankime/FCollections/'

			   scm {
				   url 'scm:git@github.com:kristiankime/FCollections.git'
				   connection 'scm:git@github.com:kristiankime/FCollections.git'
				   developerConnection 'scm:git@github.com:kristiankime/FCollections.git'
			   }

			   licenses {
				   license {
					   name 'The MIT License (MIT)'
					   url 'http://opensource.org/licenses/MIT'
					   distribution 'repo'
				   }
			   }

			   developers {
				   developer {
					   id 'Kris'
					   name 'Kristian Kime'
					   email 'kristian.kime@gmail.com'
				   }
			   }
		   }
		}
	}
}


